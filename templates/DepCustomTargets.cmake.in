# This function declares a few custom targets for the dependency dep
function(socute_configure_custom_targets_@SOCUTE_DEP@ dep)
    socute_external_build_dir("${dep}" build_dir)
    socute_external_install_prefix(install_prefix)
    set(ext_dir "${build_dir}/ext")

    # we must set a toochain file if the project needs one
    if (CMAKE_TOOLCHAIN_FILE)
        set(toolchain_cmd -DCMAKE_TOOLCHAIN_FILE="${CMAKE_TOOLCHAIN_FILE}")
    endif()

    # remove command changes in 3.17
    if (CMAKE_VERSION VERSION_LESS 3.17)
        set(rm_cmd remove_directory)
    else()
        set(rm_cmd rm -Rf)
    endif()

    # Update target
    if (NOT COMMAND update_${dep})
        add_custom_target(update_${dep}
            COMMENT "Update the package ${dep}"
            COMMAND "${CMAKE_COMMAND}" --build "${ext_dir}/build"
            VERBATIM
        )
        set_target_properties(update_${dep} PROPERTIES EXCLUDE_FROM_ALL TRUE)
    endif()

    # Reinstallation target
    if (NOT COMMAND reinstall_${dep})
        add_custom_target(reinstall_${dep}
            COMMENT "Reinstall the package ${dep}"
            COMMAND "${CMAKE_COMMAND}" -E ${rm_cmd} "${build_dir}/build" "${ext_dir}/build"
            COMMAND "${CMAKE_COMMAND}" -E make_directory "${build_dir}/build" "${ext_dir}/build"
            COMMAND "${CMAKE_COMMAND}" -G "${CMAKE_GENERATOR}" ${toolchain_cmd} -S "${ext_dir}" -B "${ext_dir}/build"
            COMMAND "${CMAKE_COMMAND}" --build "${ext_dir}/build"
            VERBATIM
        )
        set_target_properties(reinstall_${dep} PROPERTIES EXCLUDE_FROM_ALL TRUE)
    endif()
endfunction()

socute_configure_custom_targets_@SOCUTE_DEP@(@SOCUTE_DEP@)
