cmake_minimum_required(VERSION 3.14)

macro(gateau_list_append_unique lst elem)
    if (NOT "${elem}" IN_LIST ${lst})
        list(APPEND ${lst} "${elem}")
    endif()
endmacro()

function(gateau_uninstall_dep_@GATEAU_DEP_ID@ dep)
    set(install_prefix "${GATEAU_DEP_INSTALL_PREFIX}")
    set(manifest "${install_prefix}/share/gateau/manifests/${dep}_install_manifest.txt")

    # No install manifest file available, skipping uninstall
    if (NOT EXISTS "${manifest}")
        return()
    endif()

    file(STRINGS "${manifest}" files)

    # Encountered directories, we will remove them if empty
    set(dirs)

    # Delete the files, and also the manifest
    # keep track of directories
    foreach (path ${files} "${manifest}")
        get_filename_component(abs_path "${path}" ABSOLUTE BASE_DIR "${install_prefix}")
        if (IS_DIRECTORY "${abs_path}")
            gateau_list_append_unique(dirs "${abs_path}")
            continue()
        endif()

        get_filename_component(abs_dir "${abs_path}" DIRECTORY)
        gateau_list_append_unique(dirs "${abs_dir}")

        if (IS_SYMLINK "${abs_path}" OR EXISTS "${abs_path}")
            message(STATUS "Uninstalling ${abs_path}")
            file(REMOVE "${abs_path}")
        else()
            message(STATUS "File ${path} does not exist.")
        endif()
    endforeach()

    # We try to cleanup empty directories
    # Do no try to be smart about it - The list may be in any order
    while (TRUE)
        set(rm_dirs FALSE)
        set(dirs_cp ${dirs})

        foreach(dir ${dirs_cp})
            file(GLOB files_in_dir "${dir}/*")
            if (NOT files_in_dir)
                message(STATUS "Uninstalling ${dir}")
                file(REMOVE_RECURSE "${dir}")
                set(rm_dirs TRUE)

                # also process parents of this directory
                get_filename_component(parent_dir "${dir}" DIRECTORY)

                # do not descend below the install prefix
                if (parent_dir MATCHES "^${install_prefix}")
                    gateau_list_append_unique(dirs "${parent_dir}")
                endif()

                # remove from the list, last, in order to avoid potential recursive situations
                list(REMOVE_ITEM dirs "${dir}")
            endif()
        endforeach()

        # no change in a whole loop: nothing more to do
        if (NOT rm_dirs)
            break()
        endif()
    endwhile()
endfunction()

gateau_uninstall_dep_@GATEAU_DEP_ID@(@GATEAU_DEP@)
