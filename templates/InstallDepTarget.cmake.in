# This function declare both an installation and uninstallation target for the
# dependency dep
function(socute_configure_install_target_@SOCUTE_DEP@ dep)
    socute_external_build_dir("${dep}" build_dir)
    socute_external_install_dir("${dep}" install_prefix)
    set(ext_dir "${build_dir}/ext")

    # we must set a toochain file if the project needs one
    if (CMAKE_TOOLCHAIN_FILE)
        set(toolchain_cmd -DCMAKE_TOOLCHAIN_FILE="${CMAKE_TOOLCHAIN_FILE}")
    endif()

    # Uninstallation target
    if (CMAKE_VERSION VERSION_LESS 3.17)
        set(rm_cmd "remove_directory")
    else()
        set(rm_cmd "rm -Rf")
    endif()

    add_custom_target(uninstall_${dep}
        COMMENT "Uninstall the package ${dep}"
        COMMAND "${CMAKE_COMMAND}" -E "${rm_cmd}" "${build_dir}/build" "${ext_dir}/build" "${install_prefix}"
        VERBATIM
    )

    # Installation target
    add_custom_target(reinstall_${dep}
        COMMENT "Install the package ${dep}"
        COMMAND "${CMAKE_COMMAND}" -E make_directory "${build_dir}/build" "${ext_dir}/build"
        COMMAND "${CMAKE_COMMAND}" -G "${CMAKE_GENERATOR}" ${toolchain_cmd} -S "${ext_dir}" -B "${ext_dir}/build"
        COMMAND "${CMAKE_COMMAND}" --build "${ext_dir}/build"
        VERBATIM
    )
    add_dependencies(reinstall_${dep} uninstall_${dep})
endfunction()

socute_configure_install_target_@SOCUTE_DEP@(@SOCUTE_DEP@)
